/* ==================================================================================== *
 *  v4.0.0                                                                              *
 *                                                                                      *
 *        S U M M O N    B H A A L S P A W N    ( m a i n    c o m p o n e n t )        *
 *                                                                                      *
 * ==================================================================================== */

SILENT

COPY ~sbs/npcs~ ~override~

COPY ~sbs/audio~ ~override~

/* ------------------ *
 *  Installing items  *
 * ------------------ */

// Summon Bhaalspawn scroll
// ------------------------
COPY ~sbs/itm/scrlbs.itm~ ~override~
	SAY NAME2 @100
	SAY DESC @101

// Divine Soul sword and spell
// ---------------------------
COPY ~sbs/itm/sw2hds.itm~ ~override~
	SAY NAME1 @103
	SAY NAME2 @103
  LPF ~GW_WRITE_EE_ITM_DESCRIPTIONS~ STR_VAR GW_desc_to_update = "@104" END
	// SAY DESC @104

ACTION_IF (GAME_IS ~bg2ee eet~) BEGIN

PRINT ~Item restricted to Sandra for EE~
OUTER_SET Re_sw2hds = RESOLVE_STR_REF (@143)
OUTER_SET use_her = RESOLVE_STR_REF (@107)
APPEND ~ITEM_USE.2DA~ ~sw2hds   bhaalspw      %Re_sw2hds%      2           %use_her%~ UNLESS ~sw2hds~

END ELSE ACTION_IF (GAME_IS ~bg2 bgt~ AND MOD_IS_INSTALLED ~SETUP-TOBEX.TP2~ ~100~) BEGIN

PRINT ~Item restricted to Sandra for bg2~
OUTER_SET Re_sw2hds = RESOLVE_STR_REF (@143)
APPEND ~ITEM_USE.2DA~ ~sw2hds   bhaalspw      -1      2          %Re_sw2hds%~ UNLESS ~sw2hds~

END

COPY ~sbs/spin/spin135.spl~ ~override~
          LPF ALTER_SPELL_EFFECT
            INT_VAR
				match_opcode  = 139
				parameter1 =  RESOLVE_STR_REF (@105)
            STR_VAR
          END

COPY ~sbs/spin/spinds.spl~ ~override~
	SAY NAME1 @103

// Cure Light Wounds Innate Ability
// --------------------------------
COPY ~sbs/spin/spin130.spl~ ~override~
          LPF ALTER_SPELL_EFFECT
            INT_VAR
				match_opcode  = 139
				parameter1 =  RESOLVE_STR_REF (@106)
            STR_VAR
          END


/* ---------------------- *
 *  Installing creatures  *
 * ---------------------- */

// Sandra
// ------
COPY ~sbs/cre/bhaalspw.cre~ ~override~
	SAY NAME1 @107
	SAY NAME2 @107
		PATCH_IF (GAME_IS ~bg2ee eet~) BEGIN
	WRITE_ASCII 0x34 ~sandram~ (8) 
		END
//	SAY INITIAL_MEETING   ~~
 	SAY MORALE            @108
	SAY HAPPY             @109
	SAY UNHAPPY_ANNOYED   @110
	SAY UNHAPPY_SERIOUS   @111
	SAY UNHAPPY_BREAKING  @112
	SAY LEADER            @113
	SAY TIRED             @114
	SAY BORED             @115
	SAY BATTLE_CRY1       @116
	SAY BATTLE_CRY2       @117
	SAY BATTLE_CRY3       @118
	SAY BATTLE_CRY4       @119
	SAY BATTLE_CRY5       @120
	SAY DAMAGE            @121
	SAY DYING             @122
	SAY HURT              @123
	SAY AREA_FOREST       @124
	SAY AREA_CITY         @124
	SAY AREA_DUNGEON      @124
	SAY AREA_DAY          @124
	SAY AREA_NIGHT        @124
	SAY SELECT_COMMON1    @118
	SAY SELECT_COMMON2    @117
	SAY SELECT_COMMON3    @118
	SAY SELECT_COMMON4    @117
	SAY SELECT_COMMON5    @118
	SAY SELECT_COMMON6    @117
	SAY SELECT_ACTION1    @135
	SAY SELECT_ACTION2    @136
	SAY SELECT_ACTION3    @137
	SAY SELECT_ACTION4    @138
	SAY SELECT_ACTION5    @109
	SAY SELECT_ACTION6    @110
	SAY SELECT_ACTION7    @124
	SAY SELECT_RARE1      @113
	SAY SELECT_RARE2      @143
	SAY CRITICAL_HIT      @120
	SAY CRITICAL_MISS     @145
	SAY TARGET_IMMUNE     @146
	SAY INVENTORY_FULL    @147
	WRITE_LONG PICKED_POCKET (BNOT 0x0) // SAY PICKED_POCKET     ~~
	WRITE_LONG HIDDEN_IN_SHADOWS (BNOT 0x0) // SAY HIDDEN_IN_SHADOWS ~~
	SAY SPELL_DISRUPTED   @149
	WRITE_LONG SET_A_TRAP (BNOT 0x0) //SAY SET_A_TRAP        ~~
	SAY BIO               @150

// Seller Thief
// ------------
COPY ~sbs/cre/sellthf.cre~ ~override~

// Messenger
// ---------
COPY ~sbs/cre/sbsmess.cre~ ~override~
	SAY NAME1 @151
	SAY NAME2 @151


/* ---------------------- *
 *  Installing portraits  *
 * ---------------------- */

	ACTION_IF GAME_IS ~bg2ee eet~ BEGIN

COPY ~sbs/%portrait%/EE~ ~override~

	END ELSE BEGIN

COPY ~sbs/%portrait%~ ~override~

	END


/* --------------------------------- *
 *  Compiling scripts and dialogues  *
 * --------------------------------- */

COMPILE ~sbs/baf/bhaalspw.baf~

COMPILE ~sbs/d/bhaalspw.d~ USING ~weidu_external/sbs/languages/autotra/%s/bhaalspw.tra~
COMPILE ~sbs/d/sellthf.d~ USING ~weidu_external/sbs/languages/autotra/%s/sellthf.tra~
COMPILE EVALUATE_BUFFER ~sbs/d/bbhalspw.d~ USING ~weidu_external/sbs/languages/autotra/%s/bbhalspw.tra~


/* ------------------- *
 *  Extending scripts  *
 * ------------------- */

// Extend the bottom of ar0300 so the thief appears in the docks district
// ----------------------------------------------------------------------
EXTEND_BOTTOM ~ar0300.bcs~ ~sbs/baf/ar0300.baf~

// Extend the bottom of ar0903 so the messenger appears in the Radiant Heart Headquarters
// --------------------------------------------------------------------------------------
EXTEND_BOTTOM ~ar0903.bcs~ ~sbs/baf/ar0903.baf~

// Extend the bottom of Imoen2.bcs so she acquires the Bhaalspawn abilities
// ------------------------------------------------------------------------
EXTEND_BOTTOM ~imoen2.bcs~ ~sbs/baf/appimoen.baf~


/* ----------------------------------------------- *
 *  Appending Sandra's dialogue files to the game  *
 * ----------------------------------------------- */

// BG2 game without ToB
// --------------------
APPEND ~pdialog.2da~ ~bhaalspw bhalspwp bhalspwj bhalspwd~ UNLESS ~bhaalspw~ UNLESS ~25POST~

APPEND ~interdia.2da~ ~bhaalspw bbhalspw~ UNLESS ~bhaalspw~ UNLESS ~25FILE~

// ToB based game
// --------------
APPEND ~pdialog.2da~ ~bhaalspw bhalspwp bhalspwj bhalspwd bhal25p bhal25j bhal25d bhal25~ UNLESS ~bhaalspw~ IF ~25POST~

APPEND ~interdia.2da~ ~bhaalspw bbhalspw bbhalspw~ UNLESS ~bhaalspw~ IF ~25FILE~

// ----------------------------------------------------------------------
// Append rows to Interdia.2da so Interact Dialogs work
/*
APPEND ~interdia.2da~
~Imoen2	BImoen2~
UNLESS ~Imoen2~
*/

// From Bg2Fixpack
ACTION_IF GAME_IS ~bg2~ AND NOT GAME_INCLUDES ~tob~ THEN BEGIN

  APPEND ~interdia.2da~ ~IMOEN       BIMOEN2~ UNLESS ~[ %TAB%]BIMOEN2[ %TAB%%LNL%%MNL%%WNL%]~ // fixes SoA version
  
END ELSE ACTION_IF GAME_IS ~bg2~ AND GAME_INCLUDES ~tob~ BEGIN

  COPY_EXISTING ~interdia.2da~ ~override~
    SET_2DA_ENTRY 17 1 2 ~BIMOEN2~ // fixes ToB version
    BUT_ONLY
END
